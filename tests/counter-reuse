global main

%include "cabna/iface"

%assign count 100_000_000


section .text


main:  ; (done (a n))
  jmp_ret alloc_task
  mov qword [arg2_r9 + task.exec], done
  mov qword [arg2_r9 + task.need], 1
  mov r12, arg2_r9

  jmp_ret alloc_task
  mov qword [arg2_r9 + task.exec], a
  mov qword [arg2_r9 + task.arg1], count
  mov [arg2_r9 + task.rcvr], r12
  mov qword [arg2_r9 + task.ridx], 1
  mov ret_r13, exec_avail  ; Make enqueue_task return to this.
  jmp enqueue_task  ; arg2_r9 already set


done:  ; Exit program.
  mov rdi, [cet_r14 + task.arg1]
  mov rax, 60  ; exit syscall number
  syscall



; (define (a n) (b (- n 1)))
a:
  mov arg2_r9, cet_r14
  sub qword [arg2_r9 + task.arg1], 1
  mov qword [arg2_r9 + task.exec], b
  mov ret_r13, exec_avail  ; Make enqueue_task return to this.
  jmp enqueue_task  ; arg2_r9 already set



; (define (b n) (c (- n 2)))
b:
  mov arg2_r9, cet_r14
  sub qword [arg2_r9 + task.arg1], 2
  mov qword [arg2_r9 + task.exec], c
  mov ret_r13, exec_avail  ; Make enqueue_task return to this.
  jmp enqueue_task  ; arg2_r9 already set



; (define (c n) (if (<= n 0) n (a n)))
c:
  mov arg1_r8, [cet_r14 + task.arg1]
  cmp arg1_r8, 0
  jle supply_retval

  mov arg2_r9, cet_r14
  mov qword [arg2_r9 + task.exec], a
  mov ret_r13, exec_avail  ; Make enqueue_task return to this.
  jmp enqueue_task  ; arg2_r9 already set
