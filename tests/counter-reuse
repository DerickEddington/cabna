global main

%include "cabna/iface"

%assign count 100_000_000


section .text


proc main:  ; (done (a n))
  main_race

  jmp_ret alloc_task
  mov qword [arg1_rdi + task.exec], done
  mov qword [arg1_rdi + task.need], 1
  mov rbx, arg1_rdi

  jmp_ret alloc_task
  mov qword [arg1_rdi + task.exec], a
  mov qword [arg1_rdi + task.arg1], count
  mov [arg1_rdi + task.rcvr], rbx
  mov qword [arg1_rdi + task.ridx], 1
  jmp_ret_to sched_task, exec_avail  ; arg1_rdi already set


proc done:  ; Exit program.
  stat call print_stats
  mov rdi, [cet_r14 + task.arg1]
  mov eax, 231  ; exit_group syscall number
  syscall



; (define (a n) (b (- n 1)))
proc a:
  mov arg1_rdi, cet_r14
  sub qword [arg1_rdi + task.arg1], 1
  mov qword [arg1_rdi + task.exec], b
  jmp_ret_to sched_task, exec_avail  ; arg1_rdi already set



; (define (b n) (c (- n 2)))
proc b:
  mov arg1_rdi, cet_r14
  sub qword [arg1_rdi + task.arg1], 2
  mov qword [arg1_rdi + task.exec], c
  jmp_ret_to sched_task, exec_avail  ; arg1_rdi already set



; (define (c n) (if (<= n 0) n (a n)))
proc c:
  mov arg1_rdi, [cet_r14 + task.arg1]
  test arg1_rdi, arg1_rdi
  mov esi, free_pet__exec_avail
  unlikely jle supply_retval

  mov arg1_rdi, cet_r14
  mov qword [arg1_rdi + task.exec], a
  jmp_ret_to sched_task, exec_avail  ; arg1_rdi already set
